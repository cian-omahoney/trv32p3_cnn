/*
-- File : ldst.n
--
-- Contents : nML model for the trv32p3_cnn processor -- load/store instructions.
--
-- Copyright (c) 2019-2022 Synopsys, Inc. This Synopsys processor model
-- captures an ASIP Designer Design Technique. The model and all associated
-- documentation are proprietary to Synopsys, Inc. and may only be used
-- pursuant to the terms and conditions of a written license agreement with
-- Synopsys, Inc.  All other use, reproduction, modification, or distribution
-- of the Synopsys processor model or the associated  documentation is
-- strictly prohibited.
*/

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Units & Transitories
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// Load-Extension Unit (zero-/sign extension)
fu lx;
trn lxB <w08>;
trn lxH <w16>;
trn lxR <w32>;

// Address Generation Unit
fu agu;
trn aguA <w32>;       // opA
trn aguB <w32>;       // opB
trn aguR <w32>;       // agu result

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Instruction nML Rules
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Top-Level Rule for this nML file

opn ldst_instrs (
    load_instr
  | store_instr
);

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ X[] Load Operations
//
// lb  rd, imm(rs1)
// lbu rd, imm(rs1)
// lh  rd, imm(rs1)
// lhu rd, imm(rs1)
// lw  rd, imm(rs1)

opn load_instr (op: majLOAD_fn3, rd: mX1w_EX, rs1: mX3r_ID, i: c12s) {
  action {
    stage ID:           aguA = rs1;
                        aguB = i;
    // ---
    stage ID:           aguR = add (aguA,aguB) @agu;
                        dm_addr = aguR;
    // ---
    stage ID..EX:
      switch (op) {
        case lb|lbu :   dmb_rd`EX` = DMb[dm_addr`ID`]`EX`;
        case lh|lhu :   dmh_rd`EX` = DMh[dm_addr`ID`]`EX`;
        case lw     :   dmw_rd`EX` = DMw[dm_addr`ID`]`EX`;
      }
    stage EX:
      switch (op) {
        case lb  :      lxR = sext (lxB=dmb_rd) @lx;
        case lbu :      lxR = zext (lxB=dmb_rd) @lx;
        case lh  :      lxR = sext (lxH=dmh_rd) @lx;
        case lhu :      lxR = zext (lxH=dmh_rd) @lx;
        case lw  :      lxR =           dmw_rd  @lx;
      }
    // ---
    stage EX:           rd = lxR;
  }
  syntax : op PADMNM " " rd "," PADOP1 i "(" rs1 ")";
  image  : i :: rs1 :: op :: rd :: opc32.LOAD
           class(load), class(ldst);
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ X[] Store Operations
//
// sb rs1, imm(rs1)
// sh rs1, imm(rs1)
// sw rs1, imm(rs1)

opn store_instr (op: majSTORE_fn3, rs1: mX3r_ID, rs2: mX2r_EX, i: c12s) {
  action {
    stage ID:           aguA = rs1;
                        aguB = i;
    // ---
    stage ID:           aguR = add (aguA,aguB) @agu;
                        dm_addr = aguR;
    // ---
    stage ID..EX:
      switch (op) {
        case sb :       DMb[dm_addr`ID`]`EX` = dmb_wr`EX` = rs2;
        case sh :       DMh[dm_addr`ID`]`EX` = dmh_wr`EX` = rs2;
        case sw :       DMw[dm_addr`ID`]`EX` = dmw_wr`EX` = rs2;
      }
  }
  syntax : op PADMNM " " rs2 "," PADOP1 i "(" rs1 ")";
  image  : i[11..5] :: rs2 :: rs1 :: op :: i[4..0] :: opc32.STORE
           class(store), class(ldst);
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Chess Views for AGU Operations
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Indirect Addressing

chess_view () {
  aguR = add (aguA, aguB = 0);
} -> {
  aguR = aguA;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Direct Addressing

chess_view () {
  aguR = add (aguA = zero, aguB);
} -> {
  aguR = aguB;
}
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ Hardware Stalls for Load/Store Instructions
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// ~~~ AGU uses result from other units

// Hazard condition
// cycle            0  1  2  3  4  5  6  7  8
// add x5,x6,x7     IF ID EX
//                        ^^ write x5
// ld x4,4(x5)         IF ID EX
//                        ^^ read x5

hw_stall 1 cycles class(agu_read_after_write) () {
  stage EX: X$[#] = ...;
} -> {
  stage ID: x_r3 = X$[#];
}
